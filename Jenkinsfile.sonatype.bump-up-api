/*
 * Copyright (c) 2016-present Sonatype, Inc. All rights reserved.
 * Includes the third-party code listed at http://links.sonatype.com/products/nexus/attributions.
 * "Sonatype" is a trademark of Sonatype, Inc.
 */
@Library(['private-pipeline-library', 'jenkins-shared', 'int-jenkins-shared']) _
import com.sonatype.jenkins.pipeline.GitHub

String apiToken = null
withCredentials([[$class: 'UsernamePasswordMultiBinding', credentialsId: 'integrations-github-api',
                  usernameVariable: 'GITHUB_API_USERNAME', passwordVariable: 'GITHUB_API_PASSWORD']]) {
  apiToken = env.GITHUB_API_PASSWORD
}

GitHub gitHub = new GitHub(this, 'jenkinsci/nexus-platform-plugin', apiToken)

node('ubuntu-zion') {
  def platformApiVersion, branch

  stage('Preparation') {
    deleteDir()

    // Checkout and config
    checkout scm
    runSafely 'git config user.email sonatype-ci@sonatype.com'
    runSafely 'git config user.name Sonatype CI'

    // Setup common variables
    platformApiVersion = getVersionFromBuildName(env.platformApiBuild_NAME)
    branch = 'test-new-release-process'
  }
  stage('Bump Nexus Platform API Version') {
    mvn jenkinsMavenConfig(), 'org.codehaus.mojo:versions-maven-plugin:2.8.1:set-property -DgenerateBackupPoms=false ' +
        "-DnewVersion=${platformApiVersion} -Dproperty=nexus-platform-api.version"
  }
  stage('Build & Test') {
    mvn jenkinsMavenConfig(), 'clean verify -DskipTests'
  }
  stage('Run Downstream') {
    runDownstream('extra-tests')
  }
  stage('Evaluate Policies') {
    iqPolicyEvaluation(gitHub, 'release')
  }
  stage('Push code') {
    // Add and commit changes
    runSafely 'git add pom.xml'
    runSafely "git commit -m 'Bumping nexus plarform api version to ${platformApiVersion}'"

    // Push changes
    withCredentials([[$class: 'UsernamePasswordMultiBinding', credentialsId: 'integrations-github-api',
                      usernameVariable: 'GITHUB_API_USERNAME', passwordVariable: 'GITHUB_API_PASSWORD']]) {
      // Setup remote url with credentials
      runSafely "git remote set-url origin https://${env.GITHUB_API_USERNAME}:${env.GITHUB_API_PASSWORD}@github" +
          '.com/jenkinsci/nexus-platform-plugin.git'

      // Get latest commits to not have issues on push
      runSafely "git pull --no-rebase --no-edit origin ${branch}"

      // Push code changes
      runSafely "git push origin HEAD:${branch}"
    }
  }
}

String getCommitId() {
  return runSafely ('git rev-parse HEAD', true)
}

Map jenkinsMavenConfig() {
  def common = mavenCommon([mavenStandardOptions: ''])
  common.settingsFile = 'jenkins-settings.xml'
  common.mavenVersion = 'Maven 3.6.x'
  return common
}

void iqPolicyEvaluation(gitHub, stage) {
  def commitId = getCommitId()
  gitHub.statusUpdate commitId, 'pending', 'analysis', 'Nexus Lifecycle Analysis in running'

  try {
    def evaluation = nexusPolicyEvaluation failBuildOnNetworkError: false, iqApplication: 'nexus-platform-plugin',
        iqScanPatterns: [[scanPattern: 'scan_nothing']], iqStage: stage, jobCredentialsId: ''
    gitHub.statusUpdate commitId, 'success', 'analysis', 'Nexus Lifecycle Analysis passed',
        "${evaluation.applicationCompositionReportUrl}"
  }
  catch (error) {
    def evaluation = error.policyEvaluation
    gitHub.statusUpdate commitId, 'failure', 'analysis', 'Nexus Lifecycle Analysis failed',
        "${evaluation.applicationCompositionReportUrl}"
    throw error
  }
}
